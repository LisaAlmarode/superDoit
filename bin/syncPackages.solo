#!/usr/bin/env superdoit_solo
instvars
operation
converter
%
usage
-----
USAGE $basename [--help | -h]
  t2f2t.sh [--debug ] create <component-name> <project-root> <t2f2t-data-dir> <tonel-pkg-dir> <filetree-pkg-dir>

  t2f2t.sh  [--debug ] filetree <t2f2t-data-dir-path>
  t2f2t.sh  [--debug ] tonel <t2f2t-data-dir-path>

DESCRIPTION
  Copy all of the packages from one package directory to another package
  directory, converting from/to filetree/tonel along the way.

	Leverages the https://github.com/GsDevKit/T2F2T-Conversion project.


OPTIONS
  -h, --help                 display usage message
  -D, --debug                bring up topaz debugger in the event of a script error

EXAMPLES
  $basename --help
  $basename -h
  $basename --debug
  $basename -D

  $basename create SuperDoit $GS_HOME/shared/repos/superDoit packages/t2f2t packages/tonel packages/filetree
  $basename --debug create $GS_HOME/shared/repos/superDoit packages/t2f2t packages/tonel packages/filetree

  $basename filetree $GS_HOME/shared/repos/superDoit/packages/t2f2t
  $basename --debug filetree $GS_HOME/shared/repos/superDoit/t2f2t

  $basename tonel $GS_HOME/shared/repos/superDoit/packages/t2f2t
  $basename --debug tonel $GS_HOME/shared/repos/superDoit/t2f2t

-----
%
projectshome
$GS_HOME/shared/repos
%
specs
[
RwLoadSpecificationV2 {
	#projectName : 'T2F2T-Conversion',
	#gitUrl : 'file:$ROWAN_PROJECTS_HOME/T2F2T-Conversion',
	#revision : 'gemstone',
	#projectSpecFile : 'rowan/tonel.ston',
	#componentNames : [
		'Core'
	],
	#comment : 't2f2t load specification for tonel format packages'
}
]
%
method
operation
	| posArgs |
	operation 
		ifNil: [
			operation := self positionalArgs at: 1.
			(#( 'create' 'filetree' 'tonel' ) 
					includes: operation) ifFalse: [ self error: 'Unknown command ', operation printString ].
			operation = 'create'
				ifTrue: [ 
					(posArgs := self positionalArgs size) ~= 6
						ifTrue: [
							self error: 'Missing positional arguments for ', 
								operation printString, 
								' command. Expected 6 got ', 
								posArgs printString ].
					^ operation ].
			(posArgs := self positionalArgs size) ~= 2
				ifTrue: [ 
					self error: 'Missing positional arguments for ', 
						operation printString, 
						' command. Expected 2 got ', 
						posArgs printString ] ].
	^ operation
%
method
t2f2tPath
	^ self positionalArgs at: 2
%
method
componentName
	^ self positionalArgs at: 2
%
method
projectRootPath
	^ self positionalArgs at: 3
%
method
createT2f2tPath
	^ self positionalArgs at: 4
%
method
tonelPackagePath
	^ self positionalArgs at: 5
%
method
filetreePackagePath
	^ self positionalArgs at: 6
%
method
convertTo: targetFormat packagesUsing: t2f2tPath stdout: stdout
	"Derived from T2F2TConversion>>convertTo:packagesUsing:stdout: (ca853a2). A superDoit
		project is used in two very different places in GsDevKit_home and the T2F2T-Conversion
		project was written to use absolute pathc in the t2f2t meta data ... the modifications done
		here are using the fact that the t2f2tPath is an absolute path to a known location in
		this project, so the tonel and filetree load specs can use disk urls derived from 
		t2f2tPath ..."
	| resolvedProject t2f2tRoot tonelLoadSpec tonelProject filetreeLoadSpec filetreeProject targetProject sourceProject |
	t2f2tRoot := t2f2tPath asFileReference.
	t2f2tRoot exists
		ifFalse: [ 
			^ self
				error:
					'The t2f2t metadata directory ' , t2f2tRoot printString , ' does not exist.'
				to: stdout ].

	tonelLoadSpec := (RwSpecification fromFile: (t2f2tRoot / 'specs' / 'filetree.ston'))
		diskUrl: 'file://', t2f2tRoot parent parent pathString;
		yourself.
	resolvedProject := tonelLoadSpec resolve.
	tonelProject := (RwResolvedProject newNamed: resolvedProject name)
		_resolvedProject: resolvedProject resolve;
		yourself.

	filetreeLoadSpec := (RwSpecification fromFile: (t2f2tRoot / 'specs' / 'tonel.ston'))
		diskUrl: 'file://', t2f2tRoot parent parent pathString;
		yourself.
	resolvedProject := filetreeLoadSpec resolve.
	filetreeProject := (RwResolvedProject newNamed: resolvedProject name)
		_resolvedProject: resolvedProject resolve;
		yourself.

	targetFormat = 'tonel'
		ifTrue: [ 
			targetProject := tonelProject.
			sourceProject := filetreeProject ]
		ifFalse: [ 
			targetFormat = 'filetree'
				ifTrue: [ 
					targetProject := filetreeProject.
					sourceProject := tonelProject ]
				ifFalse: [ 
					self
						error:
							'unknown format ' , targetFormat printString , '. Should be [ tonel | filetree]'
						to: stdout ] ].
	converter _copyPackagesFrom: sourceProject to: targetProject
%
doit
	self preDoitSpecLoad.	"load the T2F2T-Conversion project from spec"
	converter := (self 
		globalNamed: 'T2F2TConversion' 
		ifAbsent: [ self error: 'T2F2TConversion class not loaded']) new.
	self operation = 'create'
		ifTrue: [
			converter
				createForProject: self projectRootPath 
				t2f2t: self createT2f2tPath
				tonel: self tonelPackagePath
				filetree: self filetreePackagePath
				projectName: 't2f2tMeta' 
				componentName: self componentName 
				packageConvention: 'Monticello' 
				stdout: self stdout.
			^ true ].
	self 
		convertTo: self operation 
		packagesUsing: self t2f2tPath 
		stdout: self stdout.
	^true
%

